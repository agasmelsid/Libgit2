# Select a hash backend

IF(USE_SHA1 STREQUAL ON OR USE_SHA1 STREQUAL "CollisionDetection")
	SET(SHA1_BACKEND "CollisionDetection")
ELSEIF(USE_SHA1 STREQUAL "HTTPS")
	message(STATUS "Checking HTTPS backendâ€¦ ${HTTPS_BACKEND}")
	IF(HTTPS_BACKEND STREQUAL "SecureTransport")
		SET(SHA1_BACKEND "CommonCrypto")
	ELSEIF(HTTPS_BACKEND STREQUAL "WinHTTP")
		SET(SHA1_BACKEND "Win32")
	ELSEIF(HTTPS_BACKEND)
		SET(SHA1_BACKEND ${HTTPS_BACKEND})
	ELSE()
	ENDIF()
	IF(NOT HTTPS_BACKEND)
		SET(SHA1_BACKEND "CollisionDetection")
	ENDIF()
	message(STATUS "Using SHA1 backend ${SHA1_BACKEND}")
ELSEIF(USE_SHA1 STREQUAL "Generic")
	SET(SHA1_BACKEND "Generic")
ELSE()
	MESSAGE(FATAL_ERROR "Invalid value for USE_SHA1: ${USE_SHA1}")
ENDIF()

IF(SHA1_BACKEND STREQUAL "CollisionDetection")
	SET(GIT_SHA1_COLLISIONDETECT 1 PARENT_SCOPE)
	ADD_DEFINITIONS(-DSHA1DC_NO_STANDARD_INCLUDES=1)
	ADD_DEFINITIONS(-DSHA1DC_CUSTOM_INCLUDE_SHA1_C=\"common.h\")
	ADD_DEFINITIONS(-DSHA1DC_CUSTOM_INCLUDE_UBC_CHECK_C=\"common.h\")

	FILE(GLOB SHA1DC_SOURCES hash/sha1/collisiondetect.* hash/sha1/sha1dc/*)
	ADD_LIBRARY(HashBackend STATIC ${SHA1DC_SOURCES})
ELSEIF(SHA1_BACKEND STREQUAL "OpenSSL")
	# OPENSSL_FOUND should already be set, we're checking HTTPS_BACKEND

	SET(GIT_SHA1_OPENSSL 1 PARENT_SCOPE)
	IF(CMAKE_SYSTEM_NAME MATCHES "FreeBSD")
		LIST(APPEND LIBGIT2_PC_LIBS "-lssl")
	ELSE()
		LIST(APPEND LIBGIT2_PC_REQUIRES "openssl")
	ENDIF()

	FILE(GLOB OPENSSL_SOURCES hash/sha1/openssl.*)
	ADD_LIBRARY(HashBackend STATIC ${OPENSSL_SOURCES})
ELSEIF(SHA1_BACKEND STREQUAL "CommonCrypto")
	SET(GIT_SHA1_COMMON_CRYPTO 1 PARENT_SCOPE)
	FILE(GLOB SRC_SHA1 hash/sha1/common_crypto.*)
ELSEIF(SHA1_BACKEND STREQUAL "mbedTLS")
	SET(GIT_SHA1_MBEDTLS 1 PARENT_SCOPE)

	FILE(GLOB MBEDTLS_SOURCES hash/sha1/mbedtls.*)
	ADD_LIBRARY(HashBackend STATIC ${MBEDTLS_SOURCES})
	TARGET_INCLUDE_DIRECTORIES(HashBackend SYSTEM PRIVATE ${MBEDTLS_INCLUDE_DIR})
	TARGET_LINK_LIBRARIES(HashBackend ${MBEDTLS_LIBRARIES})

	# mbedTLS has no pkgconfig file, hence we can't require it
	# https://github.com/ARMmbed/mbedtls/issues/228
	# For now, pass its link flags as our own
	LIST(APPEND LIBGIT2_PC_LIBS ${MBEDTLS_LIBRARIES})
ELSEIF(SHA1_BACKEND STREQUAL "Win32")
	SET(GIT_SHA1_WIN32 1 PARENT_SCOPE)

	FILE(GLOB WIN32_SOURCES hash/sha1/win32.*)
	ADD_LIBRARY(HashBackend STATIC ${WIN32_SOURCES})
ELSEIF(SHA1_BACKEND STREQUAL "Generic")
	FILE(GLOB GENERIC_SOURCES hash/sha1/generic.*)
	ADD_LIBRARY(HashBackend STATIC ${GENERIC_SOURCES})
ELSE()
	MESSAGE(FATAL_ERROR "Asked for unknown SHA1 backend: ${SHA1_BACKEND}")
ENDIF()

TARGET_INCLUDE_DIRECTORIES(HashBackend PRIVATE "${libgit2_SOURCE_DIR}/include"
					       "${libgit2_BINARY_DIR}/include"
					       "${libgit2_SOURCE_DIR}/src")

SET(SHA1_BACKEND ${SHA1_BACKEND} PARENT_SCOPE)

ADD_FEATURE_INFO(SHA ON "using ${SHA1_BACKEND}")
