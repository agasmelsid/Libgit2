FILE(GLOB SRC_NTLMCLIENT "ntlm.c" "util.c")
FILE(GLOB SRC_NTLMCLIENT_HEADERS "ntlm.h" "ntlmclient.h" "compat.h" "crypt.h" "util.h" "utf8.h" "unicode.h")

IF(USE_ICONV)
	FILE(GLOB SRC_NTLMCLIENT_UNICODE "unicode_iconv.c")
ELSE()
	FILE(GLOB SRC_NTLMCLIENT_UNICODE "unicode_builtin.c")
ENDIF()

LIST(APPEND SRC_NTLMCLIENT ${SRC_NTLMCLIENT_UNICODE} ${SRC_NTLMCLIENT_HEADERS})

ADD_DEFINITIONS(-DNTLM_STATIC=1)

DISABLE_WARNINGS(implicit-fallthrough)

IF(USE_HTTPS STREQUAL "SecureTransport")
	ADD_DEFINITIONS(-DCRYPT_COMMONCRYPTO)
	SET(SRC_NTLMCLIENT_CRYPTO "crypt_commoncrypto.c")
	LIST(APPEND SRC_NTLMCLIENT_CRYPTO "crypt_commoncrypto.h")
	# CC_MD4 has been deprecated in macOS 10.15.
	SET_SOURCE_FILES_PROPERTIES("crypt_commoncrypto.c" COMPILE_FLAGS "-Wno-deprecated")
ELSEIF(USE_HTTPS STREQUAL "OpenSSL")
	ADD_DEFINITIONS(-DCRYPT_OPENSSL)
	INCLUDE_DIRECTORIES(${OPENSSL_INCLUDE_DIR})
	SET(SRC_NTLMCLIENT_CRYPTO "crypt_openssl.c")
ELSEIF(USE_HTTPS STREQUAL "mbedTLS")
	ADD_DEFINITIONS(-DCRYPT_MBEDTLS)
	INCLUDE_DIRECTORIES(${MBEDTLS_INCLUDE_DIR})
	SET(SRC_NTLMCLIENT_CRYPTO "crypt_mbedtls.c")
ELSE()
	MESSAGE(FATAL_ERROR "Unable to use libgit2's HTTPS backend (${USE_HTTPS}) for NTLM crypto")
ENDIF()

LIST(APPEND SRC_NTLMCLIENT ${SRC_NTLMCLIENT_CRYPTO})
LIST(SORT SRC_NTLMCLIENT)

ADD_LIBRARY(ntlmclient OBJECT ${SRC_NTLMCLIENT})
