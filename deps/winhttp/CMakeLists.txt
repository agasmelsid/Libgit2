FIND_PROGRAM(DLLTOOL dlltool CMAKE_FIND_ROOT_PATH_BOTH)
IF(NOT DLLTOOL)
	MESSAGE(FATAL_ERROR "Could not find dlltool command")
ENDIF()

IF(CMAKE_SIZEOF_VOID_P EQUAL 8)
	SET(WINHTTP_DEF "${CMAKE_CURRENT_SOURCE_DIR}/winhttp64.def")
ELSE()
	SET(WINHTTP_DEF "${CMAKE_CURRENT_SOURCE_DIR}/winhttp.def")
ENDIF()
SET(WINHTTP_LIB "${CMAKE_CURRENT_BINARY_DIR}/libwinhttp.a")

ADD_CUSTOM_COMMAND(OUTPUT ${WINHTTP_LIB}
		   COMMAND ${DLLTOOL} -d ${WINHTTP_DEF} -k -D winhttp.dll -l ${WINHTTP_LIB}
		   DEPENDS ${WINHTTP_DEF})
ADD_CUSTOM_TARGET(libwinhttp DEPENDS ${WINHTTP_LIB})

ADD_LIBRARY(WinHTTP INTERFACE)
ADD_DEPENDENCIES(WinHTTP libwinhttp)
TARGET_LINK_LIBRARIES(WinHTTP INTERFACE ${WINHTTP_LIB} rpcrt4 crypt32 ole32)
TARGET_INCLUDE_DIRECTORIES(WinHTTP INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})
